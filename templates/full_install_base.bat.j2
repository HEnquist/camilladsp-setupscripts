{# Base template for full install on Windows -#}
@echo off
setlocal EnableDelayedExpansion

set "ARCHIVE_NAME=camilladsp-windows-amd64.zip"

if "%~1"=="" (
    set "INSTALL_ROOT=%USERPROFILE%\camilladsp"
) else (
    set "INSTALL_ROOT=%~1"
)

echo --- Create folders
if not exist "%INSTALL_ROOT%\configs" mkdir "%INSTALL_ROOT%\configs"
if not exist "%INSTALL_ROOT%\coeffs" mkdir "%INSTALL_ROOT%\coeffs"
if not exist "%INSTALL_ROOT%\bin" mkdir "%INSTALL_ROOT%\bin"
if not exist "%INSTALL_ROOT%\gui" mkdir "%INSTALL_ROOT%\gui"
if not exist "%INSTALL_ROOT%\temp" mkdir "%INSTALL_ROOT%\temp"

cd %INSTALL_ROOT%\temp

echo --- Download Camillagui
if exist camillagui.zip (
    del camillagui.zip
)
powershell Invoke-WebRequest https://github.com/HEnquist/camillagui-backend/releases/download/{{ camillagui_tag }}/camillagui.zip -OutFile .\camillagui.zip
if !errorlevel! neq 0 (
    echo Failed to download gui
    exit /b 1
)

echo --- Uncompress Camillagui to %INSTALL_ROOT%\gui
powershell Expand-Archive camillagui.zip -DestinationPath "%INSTALL_ROOT%\gui" -Force
if !errorlevel! neq 0 (
    echo Failed to uncompress gui
    exit /b 1
)

echo --- Download CamillaDSP binary
if exist %ARCHIVE_NAME% (
    echo Deleting existing camilladsp archive
    del %ARCHIVE_NAME%
)
powershell Invoke-WebRequest https://github.com/HEnquist/camilladsp/releases/download/{{ camilladsp_tag }}/%ARCHIVE_NAME% -OutFile .\%ARCHIVE_NAME%
if !errorlevel! neq 0 (
    echo Failed to download camilladsp binary
    exit /b 1
)

echo --- Uncompress CamillaDSP binary
powershell Expand-Archive "%ARCHIVE_NAME%" -DestinationPath "%INSTALL_ROOT%\bin" -Force
if !errorlevel! neq 0 (
    echo Failed to uncompress binary
    exit /b 1
)

{% block setup_python_env %}
echo Assuming Python environment is already set up
{% endblock setup_python_env %}

echo.
echo All done!
echo Some temporary files were stored in %INSTALL_ROOT%\temp
echo These are no longer needed and can be deleted.

endlocal
